plugins {
    id 'org.asciidoctor.convert' version '2.4.0'
    id 'org.springframework.boot' version '2.6.1'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
    id 'com.adarshr.test-logger' version '1.2.0'
    id 'org.kordamp.gradle.livereload' version '0.2.1'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.kordamp.gradle.livereload'

group = 'com.boclips'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url = uri("https://repo.spring.io/libs-release/") }
    maven {
        url 'https://jitpack.io'
        credentials { username authToken }
    }
}

ext {
    set('snippetsDir', file('build/generated-snippets'))
    junitVersion = '5.3.1'
    kotlinVersion = '1.6.10'
    feignVersion = '11.7'
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-hateoas")

    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    implementation 'com.github.kittinunf.fuel:fuel:2.2.1'
    implementation 'com.github.kittinunf.fuel:fuel-jackson:2.2.1'
    implementation 'com.github.kittinunf.fuel:fuel-coroutines:2.2.1'

    implementation 'org.imsglobal:basiclti-util:1.1.2'

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    testImplementation 'com.github.boclips:videos:v1255'
    testImplementation 'com.github.boclips:alignment-service:v115'
    // Below are transitive client dependencies that we need to import ourselves at the moment...
    testImplementation('com.fasterxml.jackson.core:jackson-databind:2.13.1')
    testImplementation('com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1')
    testImplementation "io.github.openfeign:feign-jackson:$feignVersion"
    testImplementation "io.github.openfeign:feign-core:$feignVersion"
    testImplementation "io.github.openfeign:feign-okhttp:$feignVersion"

    testImplementation "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1"
    testImplementation('org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE')
    testImplementation('com.damnhandy:handy-uri-templates:2.1.8')
    testImplementation 'org.springframework.boot:spring-boot-starter-hateoas'
    testImplementation 'org.springframework.hateoas:spring-hateoas'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
}

test {
    outputs.dir snippetsDir
}

asciidoctorj {
    version = '2.5.2'
}

asciidoctor {
    attributes 'highlightjs-theme=github'
    inputs.dir snippetsDir
    dependsOn test
    resources {
        from('src/docs/asciidoc/resources') {
            include("favicon.ico")
        }
    }
}

task copyRestDocs(type: Copy) {
    dependsOn asciidoctor
    from "${asciidoctor.outputDir}/html5"
    into "${sourceSets.main.output.resourcesDir}/static/docs"
}

bootJar {
    dependsOn copyRestDocs
}

liveReload {
    port 8080
    docRoot "${asciidoctor.outputDir}/html5"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}
